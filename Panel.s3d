#include "Tools/ToolIcon.s3d"
#include "Tools/Tool.s3d"

/*
 * This class has to be considered as an abstract class: it must be inherited by other classes. It handles the generic things of a panel
 * in the menu, such as the shape, the positioning of the icons, the search for intersections with the ray of the action controller
 */
class Panel
{
	// Reference to the action controller. Used to pass it to the tools to update their logic
	var mActionController;
	
	// Sizes of the panel
	var mToolPanelWidth;
	var mToolPanelHeight;
	var mToolPanelSilhouetteTickness;
	
	// These variables contain the x coordinates of the range where the icons are drawn in the panel (in the local space of the panel 
	// itself). They're needed to define the range along the x axis where the icons are put inside the panel
	var mMinIconRange;
	var mMaxIconRange;
	
	// Obj of the panel
	var mPanel;
	
	// Text object containing the name of the panel
	var mPanelName;
	
	// This array contains the list of icons in the panel. It's organized as a list of lists: each list is a row in the panel
	var mTools;
	
	// Animator object to create smooth transitions
	var mAnimator;
	
	// When the user is pointing with the action controller over an icon, this variable holds the reference to the tool's icon
	var mCurrentHoveredTool;
	
	// When the user selects an icon with the action controller, this variable holds the reference to the tool's icon
	var mCurrentlySelectedTool;
	
	// Boolean that is true if a tool in this panel is selected
	var mIsToolSelected;
	
	// This is a reference to the ActionsListManager object that is in charge of the undo/redo mechanism
	var mActionsListManager;
	
	
	// This method works as the constructor for the class: the classes that will inherit from this class will need to call
	// this in their constructor as the first call
	Init(panelName, actionController, animator, actionsListManager);
	
	// Public functions (protected)
	Draw();
	GetWidth();
	GetHeight();
	FindHoveredIcon(rayOrigin, rayEnd);
	SelectCurrentHoveredTool();
	DeSelectCurrentTool();
	GenerateIconsPositions();
	SetActionAvailable(actionAvailable);
	
	// Public functions that need to be overwritten; the classes that will inherit from this class will still need to call
	// this method in their override function as the first call, to update general things of the panel
	UpdateLogic(dt);
	
	// Common functions
	GetObj();
	AddChild(child, x, y, z);
	SetPosition(position);
	Translate(transl);
	GetPosition();
	SetRotationMatrix(matrix);
	GetRotationMatrix();
	SetPivotPoint(point);
	Rotate(angle, x, y, z);
	SetRotation(angle, x, y, z);
	LocalToWorld(point);
	Hide();
	Unhide();
	SetScale(x, y, z);
	IsVisible();
	IsCollidingBBox(object);
	IsColliding(rayOrigin, rayEnd, contactPoint, contactNormal);
};


/*
 * This method works as the constructor. 
 * 
 * panelName: string containing the name of the panel
 * actionController: ActionController reference
 * animator: Animator reference
 * actionsListManager: ActionsListManager reference
 */
function Panel::Init(panelName, actionController, animator, actionsListManager)
{
	mActionController = actionController;
	mAnimator = animator;
	mActionsListManager = actionsListManager;
	
	// Sizes of the panel
	mToolPanelSilhouetteTickness = 0.02 * SCALE_FACTOR;
	mToolPanelWidth = 1.0 * SCALE_FACTOR;
	mToolPanelHeight = 1.0 * SCALE_FACTOR;
	
	// Horizzontal range in which the tools' icons need to be placed
	mMinIconRange = -0.5 * SCALE_FACTOR;
	mMaxIconRange = 0.5 * SCALE_FACTOR;
	
	// Creating the panel object with the respective function from the Utils class. I rotate it
	mPanel = CreateGlassPlane(mToolPanelWidth, mToolPanelHeight, 0.01 * SCALE_FACTOR, [1.0, 1.0, 1.0], 0.5);
	
	// Text with the name of the panel
	mPanelName = Text(panelName, 0.3, [1, 1, 1]);
	mPanel.AddChild(mPanelName.GetObj(), -0.05 * SCALE_FACTOR, mToolPanelHeight / 2.0 - mPanelName.GetFontHeight(), 0.001 * SCALE_FACTOR);

	// Initializing the array that will hold the icons for the tools. It will be filled up by the class that will inherit this one
	mTools = {};
	
	// No hovered and selected toll at the beginning
	mCurrentHoveredTool = Void;
	mCurrentlySelectedTool = Void;
	mIsToolSelected = false;
}

/*
 * This method updates the logic of the panel; in particular updates the position of its elements
 */
function Panel::UpdateLogic(dt)
{
	var toolsRows = len(mTools);
	
	var initialYoffset = 0.1 * SCALE_FACTOR;
	
	// If no tool is selected in the panel I update the position of the tools' icons
	if(!mIsToolSelected)
	{
		// I need to position every icon, so I iterate over all of the rows of icons
		for(var i = 0; i < toolsRows; i++)
		{
			var toolRow = mTools[i];
			var toolsInRow = len(toolRow);
			
			// For each row, I iterate over every icon in it
			for(var j = 0; j < toolsInRow; j++)
			{
				var toolIcon = toolRow[j].GetIconObj();
	
				// Finally, I can set the position and orientation of the icon (or actually of its collision 
				// object; more details in the ToolIcon class)
				toolIcon.SetCollisionPosition(mPanel.LocalToWorld(toolIcon.GetDefaultPosition()));
				toolIcon.SetCollisionRotationMatrix(mPanel.GetRotationMatrix());
			}
		}
	}
	// If a tool is selected I update its logic
	else
	{
		// Since when a tool is selected I do the scaling animation with the other icons and the panel's name, I need to
		// hide these objects when the scaling is done, i.e. when the scale reaches 0. So I check the visibility of the text
		// and I assume that when its scale is 0 also the icons' are 0
		if(mPanelName.IsVisible() && mPanelName.GetScale().x <= 0)
		{
			mPanelName.Hide();
			
			for(var i = 0; i < len(mTools); i++)
			{
				var toolRow = mTools[i];
				
				for(var j = 0; j < len(toolRow); j++)
				{
					var toolIcon = toolRow[j].GetIconObj();
					
					// I hide the icon if it's not the current one selected
					if(toolIcon != mCurrentlySelectedTool.GetIconObj())
						toolIcon.Hide();
				}
			}
		}
		
		// I update the logic of the currently active tool
		mCurrentlySelectedTool.UpdateLogic(dt);
	}
}

/*
 * This method is onyl called by the ToolsMenu class if there is an intersection of a ray inside the panel, i.e.
 * the action controller is directly pointing this panel. This method has the task of finding WHERE in the panel the ray is hitting
 */
function Panel::FindHoveredIcon(rayOrigin, rayEnd)
{
	var found = false;
	
	// I look for the hovered icon only if there is no tool active in this panel
	if(!mIsToolSelected)
	{
		// I need to check if the ray is pointing any icon
		for(var i = 0; i < len(mTools) && !found; i++)
		{
			var toolRow = mTools[i];
			
			for(var j = 0; j < len(toolRow) && !found; j++)
			{
				var toolIcon = toolRow[j].GetIconObj();
				
				// If this icon is being hit, we're done
				if(toolIcon.IsColliding(rayOrigin, rayEnd))
				{
					found = true;
					
					// If this icon wasn't being hovered, now it is. So I save this information and create an animation
					if(toolRow[j] != mCurrentHoveredTool)
					{
						mCurrentHoveredTool = toolRow[j];
						mAnimator.CreateScalingTransition(toolIcon.GetObj(), toolIcon.GetScale(), [1.35, 1.35, 1], 0.1, false);	
						
						// I show the icon's "tooltip"
						toolIcon.SetIconNameVisibility(true);
					}
				}
			}
		}
		
		// If no icon was hit and there was an icon hovered, now it's not anymore, so I save this fact and create an animation
		if(!found && mCurrentHoveredTool != Void)
		{
			var toolIcon = mCurrentHoveredTool.GetIconObj();
			mAnimator.CreateScalingTransition(toolIcon.GetObj(), toolIcon.GetScale(), [1, 1, 1], 0.1, false);	
			toolIcon.SetIconNameVisibility(false);
			mCurrentHoveredTool = Void;
		}
	}
	else
		mCurrentHoveredTool = Void;
}

/*
 * This method activates the current hovered tool, if there was any.
 * 
 * Returns true if a tool was selected, false otherwise
 */
function Panel::SelectCurrentHoveredTool()
{
	// I only procede if there was an hovered tool
	if(mCurrentHoveredTool != Void)
	{
		var hoveredToolIcon = mCurrentHoveredTool.GetIconObj();
		
		// I hide the icon's "tooltip"
		hoveredToolIcon.SetIconNameVisibility(false);
		
		// I create a scaling transition for the panel's name to make it disappear
//		mPanelName.SetPivotPoint(mPanelName.LocalToWorld([-mPanelName.GetWidthOfLine(0) / 2.0, 0, 0]));
		mAnimator.CreateScalingTransition(mPanelName.GetObj(), mPanelName.GetScale(), [0, 1, 0], 0.2, false, 2);	
		
		// I create a scaling transition for every icon
		for(var i = 0; i < len(mTools); i++)
		{
			var toolRow = mTools[i];
			
			for(var j = 0; j < len(toolRow); j++)
			{
				var toolIcon = toolRow[j].GetIconObj();
				
				if(toolIcon != mCurrentHoveredTool)
					mAnimator.CreateScalingTransition(toolIcon.GetObj(), toolIcon.GetScale(), [0, 0, 0], 0.2, false, 2);	
			}
		}
		
		// I state that the tool hovered is now active
		mCurrentHoveredTool.SetToolActive(true);
		
		// Since the tool's icon was hovered it was bigger, so I scale it down to default
		mAnimator.CreateScalingTransition(hoveredToolIcon.GetObj(), hoveredToolIcon.GetScale(), [1, 1, 1], 0.2, false, 2);	
		
		// I create a transition to bring the icon up
		var endPos = [-GetWidth() / 2.0 + hoveredToolIcon.GetWidth(), GetHeight() / 2.0 - hoveredToolIcon.GetHeight(), 0.001 * SCALE_FACTOR];	
		mAnimator.CreateTransition(hoveredToolIcon, hoveredToolIcon.GetPosition(), endPos, 0.3, false, 2);
		
		// I save the fact that in this panel a tool is active
		mIsToolSelected = true;
		
		// I save the reference to the active tool
		mCurrentlySelectedTool = mCurrentHoveredTool;
	}
	
	return mIsToolSelected;
}

/*
 * This method deactivates the current active tool, if there was any. It's analogous to the "SelectCurrentTool" method
 * 
 * Returns true if a tool was deselected, false otherwise
 */
function Panel::DeselectCurrentTool()
{
	if(mIsToolSelected)
	{
		mIsToolSelected = false;
		
		mAnimator.CreateScalingTransition(mPanelName.GetObj(), mPanelName.GetScale(), {mPanelName.GetScale(), [1, 1, 1]}, 0.2, false, 2);
		mPanelName.Unhide();
		
		for(var i = 0; i < len(mTools); i++)
		{
			var toolRow = mTools[i];
			
			for(var j = 0; j < len(toolRow); j++)
			{
				var toolIcon = toolRow[j].GetIconObj();
				
				if(toolIcon != mCurrentlySelectedTool)
				{
					mAnimator.CreateScalingTransition(toolIcon.GetObj(), toolIcon.GetScale(), {toolIcon.GetScale(), [1, 1, 1]}, 0.2, false, 2);
					toolIcon.Unhide();
				}	
			}
		}
		
		mCurrentlySelectedTool.SetToolActive(false);
		
		var hoveredToolIcon = mCurrentlySelectedTool.GetIconObj();
		
		mAnimator.CreateTransition(hoveredToolIcon, hoveredToolIcon.GetPosition(), hoveredToolIcon.GetDefaultPosition(), 0.3, false, 2);
		
		mCurrentHoveredTool = Void;
		mCurrentlySelectedTool = Void;
	}
	
	return mIsToolSelected;
}

/*
 * This method draws the tool's icons and the panel
 */
function Panel::Draw()
{
	// Drawing each icon
	for(var i = 0; i < len(mTools); i++)
	{
		var toolRow = mTools[i];
		
		for(var j = 0; j < len(toolRow); j++)
			toolRow[j].Draw();
	}
	
	// Drawing the panel itself; I draw it last so that it can render the opacity right
	mPanel.Draw();
}

/*
 * This method MUST be called by the class the inherits from Panel in its constructor, at the end. Basically what it does
 * is compute the position of each tool's icon in the panel according to the number of tools displayed in each row, and
 * saves the position (known as "default position") in each ToolIcon object
 */
function Panel::GenerateIconsPositions()
{
	var toolsRows = len(mTools);
	var initialYoffset = 0.1 * SCALE_FACTOR;
	
	// I need to position every icon, so I iterate over all of the rows of icons
	for(var i = 0; i < toolsRows; i++)
	{
		var toolRow = mTools[i];
		var toolsInRow = len(toolRow);
		
		// For each row, I iterate over every icon in it
		for(var j = 0; j < toolsInRow; j++)
		{
			var toolIcon = toolRow[j].GetIconObj();
			
			// To find the x coordinate (in the local space of the panel) I use this formula that takes into account the whole range
			// in which icons are allowed to be (found by writing examples with pen and paper)
			var x = (Abs(mMinIconRange) + Abs(mMaxIconRange)) / (toolsInRow + 1) * (j + 1) + mMinIconRange;
			
			// The y coordinate takes into account the current number of the row we're considering; I add an initial offset to this
			var y = initialYoffset - (toolIcon.GetHeight() + 0.05 * SCALE_FACTOR) * i;

			// Finally, I can set the position and orientation of the icon
			toolIcon.SetDefaultPosition([x, y, 0.001 * SCALE_FACTOR]);
			
			mPanel.AddChild(toolIcon.GetObj(), x, y, 0.001 * SCALE_FACTOR);
		}
	}
}

/*
 * This method tells the currently active tool (if there is one) that it can/cannot do actions. It's different from the activation
 * or deactivation of a tool; for example in the notes tool when the controller is pointing the menu the tool is active but
 * it can't do any actions, meaning that it can't place notes
 */
function Panel::SetActionAvailable(actionAvailable)
{
	if(mIsToolSelected)
		mCurrentlySelectedTool.SetActionAvailable(actionAvailable);
}

/*
 * This method returns the width of the panel (which includes the edge thickness)
 */
function Panel::GetWidth()
{
	return mToolPanelWidth + mToolPanelSilhouetteTickness;
}

/*
 * This method returns the height of the panel (which includes the edge thickness)
 */
function Panel::GetHeight()
{
	return mToolPanelHeight + mToolPanelSilhouetteTickness;
}

function Panel::IsColliding(rayOrigin, rayEnd, contactPoint, contactNormal)
{
	return mPanel.IsColliding(rayOrigin, rayEnd, &contactPoint, &contactNormal);
}

function Panel::GetObj()
{
	return mPanel;
}

function Panel::AddChild(child, x, y, z)
{
	mPanel.AddChild(child, x, y, z);
}

//function Panel::SetActionAvailable(isActionAvailable)
//{
//	mViveController.SetActionAvailable(isActionAvailable);
//}

function Panel::SetPosition(position)
{
	mPanel.SetPosition(position);
}

function Panel::GetPosition()
{
	return mPanel.GetPosition();
}

function Panel::SetRotationMatrix(matrix)
{
	mPanel.SetRotationMatrix(matrix);
}

function Panel::GetRotationMatrix()
{
	return mPanel.GetRotationMatrix();
}

function Panel::SetPivotPoint(point)
{
	mPanel.SetPivotPoint(point);
}


function Panel::Rotate(angle, x, y, z)
{
	mPanel.Rotate(angle, x, y, z);
}

function Panel::SetRotation(angle, x, y, z)
{
	mPanel.SetRotation(angle, x, y, z);
}

function Panel::LocalToWorld(point)
{
	return mPanel.LocalToWorld(point);
}

function Panel::Hide()
{
	mPanel.Hide();
}

function Panel::Unhide()
{
	mPanel.Unhide();
}

function Panel::SetScale(x, y, z)
{
	mPanel.SetScale(x, y, z);
}

//function Panel::IsVisible()
//{
//	return mViveController.IsVisible();
//}

function Panel::Translate(transl)
{
	mPanel.Translate(transl);
}

function Panel::IsCollidingBBox(object)
{
	return mPanel.IsCollidingBBox(object);
}
